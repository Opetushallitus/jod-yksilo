plugins {
  id 'java'
  id 'jacoco'

  id 'org.springframework.boot' version '3.4.4'
  id 'io.spring.dependency-management' version '1.1.7'

  id 'checkstyle'
  id "com.diffplug.spotless" version "7.0.2"
  id "com.github.spotbugs" version "6.1.7"
  id "org.sonarqube" version "6.1.0.5360"
  id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'

  id "com.github.ben-manes.versions" version "0.52.0"
  id 'org.cyclonedx.bom' version '2.2.0'
}

group = 'fi.okm.jod'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

compileJava {
  options.compilerArgs << '-Xlint:all,-processing,-serial'
}

repositories {
  mavenCentral()
  maven { url = "https://build.shibboleth.net/nexus/content/repositories/releases/" }
}

ext {
  spotbugsVersion = "4.9.3"
}

configurations {
  compileClasspath {
    resolutionStrategy.activateDependencyLocking()
  }
  runtimeClasspath {
    resolutionStrategy.activateDependencyLocking()
  }
  annotationProcessor {
    resolutionStrategy.activateDependencyLocking()
  }
  schemaSpy
  mockitoAgent
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

  implementation 'org.springframework.session:spring-session-data-redis'

  implementation platform('io.awspring.cloud:spring-cloud-aws-dependencies:3.3.0')
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

  implementation 'org.springframework.security:spring-security-saml2-service-provider'

  implementation 'io.micrometer:micrometer-tracing-bridge-brave'
  implementation 'io.micrometer:micrometer-registry-cloudwatch2'
  implementation 'io.zipkin.aws:brave-propagation-aws'

  implementation platform('software.amazon.awssdk:bom:2.31.12')
  implementation 'software.amazon.awssdk:http-auth-aws'
  implementation 'software.amazon.awssdk:rds'
  implementation 'software.amazon.awssdk:sagemakerruntime'
  implementation 'software.amazon.awssdk:cloudwatch'

  implementation 'com.google.guava:guava:33.4.6-jre'

  implementation "org.flywaydb:flyway-core"
  implementation "org.flywaydb:flyway-database-postgresql"

  // Logging in JSON format
  implementation 'net.logstash.logback:logstash-logback-encoder:8.0'

  // Caffeine cache implementation
  implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'

  //override OpenSAML dependencies
  implementation 'org.bouncycastle:bcpkix-jdk18on:1.80'
  implementation 'org.bouncycastle:bcutil-jdk18on:1.80'
  implementation 'org.bouncycastle:bcprov-jdk18on:1.80'

  // OpenAPI API description and Swagger UI
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.8.6'
  developmentOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'

  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

  runtimeOnly 'org.postgresql:postgresql'

  annotationProcessor 'org.springframework:spring-context-indexer'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.hibernate.orm:hibernate-jpamodelgen'
  annotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.springframework.security:spring-security-test'

  testImplementation "org.testcontainers:postgresql"

  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.junit.jupiter:junit-jupiter'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  testAnnotationProcessor 'org.projectlombok:lombok'

  testCompileOnly 'org.projectlombok:lombok'

  // spotbugs
  implementation "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"

  schemaSpy 'org.schemaspy:schemaspy:7.0.2'
  schemaSpy 'org.postgresql:postgresql'

  mockitoAgent('org.mockito:mockito-core') {
    transitive = false
  }

}

tasks.named('test', Test) {
  useJUnitPlatform()
  jvmArgs += [
      '-XX:+EnableDynamicAgentLoading',
      "-javaagent:${configurations.mockitoAgent.asPath}"
  ]
  systemProperty('file.encoding', 'UTF-8')
  testLogging {
    events("passed", "skipped", "failed")
  }
}

checkstyle {
  toolVersion = '10.17.0'
  showViolations = false

  def archive = configurations.checkstyle.filter {
    it.name.startsWith("checkstyle")
  }
  config = resources.text.fromArchiveEntry(archive, "google_checks.xml")
}

spotless {
  java {
    target 'src/*/**/*.java'
    googleJavaFormat()
    licenseHeader '''\
    /*
     * Copyright (c) $YEAR The Finnish Ministry of Education and Culture, The Finnish
     * The Ministry of Economic Affairs and Employment, The Finnish National Agency of
     * Education (Opetushallitus) and The Finnish Development and Administration centre
     * for ELY Centres and TE Offices (KEHA).
     *
     * Licensed under the EUPL-1.2-or-later.
     */

     '''.stripIndent()
  }
}

jacoco {
  toolVersion = "0.8.12"
}

test.finalizedBy jacocoTestReport

jacocoTestReport {
  reports.xml.required = true
  reports.html.required = true
}

spotbugs {
  toolVersion = spotbugsVersion
  excludeFilter = file('gradle/config/spotbugs/exclusions.xml')
}

spotbugsMain {
  reports {
    html {
      required = true
    }
    xml {
      required = true
    }
  }
}

spotbugsTest.enabled = false

bootJar {
  manifest {
    attributes('Implementation-Title': "${rootProject.name}",
        'Implementation-Version': "${project.version}")
  }
}

springBoot {
  buildInfo {
    excludes = ['time']
  }
}

tasks.register('installGitHooks', Copy) {
  from("${rootProject.rootDir}/scripts/git-hooks") {
    include 'pre-*'
  }
  into "${rootProject.rootDir}/.git/hooks"
  filePermissions { unix(0775) }
}

if (System.getenv("CI") == null) {
  build {
    dependsOn 'installGitHooks'
  }
}

sonar {
  properties {
    property "sonar.projectKey", "Opetushallitus_jod-yksilo"
    property "sonar.organization", "opetushallitus"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.java.spotbugs.reportPaths", "build/reports/spotbugs/main.xml"
    property "sonar.cpd.exclusions", "src/**/entity/*.java"
  }
}

openApi {
  def port = new ServerSocket(0).withCloseable { socket -> socket.getLocalPort() }
  outputDir = file("${projectDir}/build/openapi")
  apiDocsUrl = "http://localhost:${port}/yksilo/openapi/openapi.json"
  waitTimeInSeconds = 30
  customBootRun {
    environment = [COMPOSE_PROJECT_NAME: "jod-yksilo-openapi"]
    args = ["--spring.main.lazy-initialization=true",
            "--server.port=${port}",
            "--logging.level.root=ERROR",
            "--logging.level.sql=OFF",
            "--logging.level.fi.okm.jod=ERROR",
            "--spring.config.additional-location=${projectDir}/src/test/resources/application-default.yml",
            "--spring.docker.compose.file=${projectDir}/compose-test.yml",
            "--spring.docker.compose.stop.command=stop",]
  }
}

tasks.named("dependencyUpdates").configure {
  checkConstraints = true
  checkBuildEnvironmentConstraints = true
  rejectVersionIf {
    it.candidate.version.containsIgnoreCase("alpha") || it.candidate.version.containsIgnoreCase("beta") || it.candidate.version.matches(".*-M\\d+")
  }
}

cyclonedxBom {
  includeConfigs = ["runtimeClasspath"]
}

tasks.register("generateSchemaDocs", JavaExec) {
  classpath = configurations.schemaSpy
  mainClass = 'org.schemaspy.Main'
  outputs.dir("build/schemaspy")
  args = ['-t', 'pgsql11',
          '-host', 'localhost',
          '-port', '45432',
          '-db', 'yksilo',
          '-u', 'postgres',
          '-p', 'postgres',
          '-s', 'yksilo',
          '-o', 'build/schemaspy',
          '-imageformat', 'svg',
          '-hq',
          '-vizjs']

  doFirst {
    def port = new ServerSocket(0).withCloseable { socket -> socket.getLocalPort() }
    def builder = new ProcessBuilder(javaLauncher.get().executablePath.toString(),
        "-cp",
        bootRun.classpath.asPath,
        bootRun.mainClass.get(),
        "--spring.main.lazy-initialization=true",
        "--spring.docker.compose.file=${projectDir}/compose-test.yml",
        "--spring.docker.compose.stop.command=down",
        "--server.port=${port}")
        .redirectError(file("build/tmp/application-error.log"))
        .redirectOutput(file("build/tmp/application-output.log"))
    builder.environment().put("COMPOSE_PROJECT_NAME", "jod-yksilo-schema")
    ext.process = builder.start()
    sleep(20000)
  }
  dependsOn tasks.resolveMainClassName, tasks.compileJava
  doLast {
    ext.process.destroy()
  }
}
