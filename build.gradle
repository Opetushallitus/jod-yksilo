plugins {
  id 'java'
  id 'jacoco'

  id 'org.springframework.boot' version '3.3.3'
  id 'io.spring.dependency-management' version '1.1.6'

  id 'checkstyle'
  id "com.diffplug.spotless" version "6.25.0"
  id "com.github.spotbugs" version "6.0.22"
  id "org.sonarqube" version "5.1.0.4882"
  id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'

  id "io.snyk.gradle.plugin.snykplugin" version "0.6.1"
  id "com.github.ben-manes.versions" version "0.51.0"
}

group = 'fi.okm.jod'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

compileJava {
  options.compilerArgs << '-Xlint:all,-processing,-serial'
}

repositories {
  mavenCentral()
  maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" }
}

ext {
  spotbugsVersion = "4.8.6"
  set("flyway.version", "10.17.2")
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation 'org.springframework.session:spring-session-data-redis'

  implementation platform('io.awspring.cloud:spring-cloud-aws-dependencies:3.1.1')
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

  implementation 'org.springframework.security:spring-security-saml2-service-provider'

  implementation 'io.micrometer:micrometer-tracing-bridge-brave'
  implementation 'io.micrometer:micrometer-registry-cloudwatch2'
  implementation 'io.zipkin.aws:brave-propagation-aws'

  implementation platform('software.amazon.awssdk:bom:2.27.24')
  implementation 'software.amazon.awssdk:http-auth-aws'
  implementation 'software.amazon.awssdk:rds'
  implementation 'software.amazon.awssdk:sagemakerruntime'
  implementation 'software.amazon.awssdk:cloudwatch'

  implementation 'com.google.guava:guava:33.3.0-jre'

  implementation "org.flywaydb:flyway-core"
  implementation "org.flywaydb:flyway-database-postgresql"

  // Logging in JSON format
  implementation 'net.logstash.logback:logstash-logback-encoder:8.0'

  // OpenAPI API description and Swagger UI
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.6.0'
  developmentOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

  runtimeOnly 'org.postgresql:postgresql'

  annotationProcessor 'org.springframework:spring-context-indexer'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.hibernate.orm:hibernate-jpamodelgen'
  annotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.springframework.security:spring-security-test'

  testImplementation "org.testcontainers:postgresql"

  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.junit.jupiter:junit-jupiter'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  testAnnotationProcessor 'org.projectlombok:lombok'

  testCompileOnly 'org.projectlombok:lombok'

  // spotbugs
  implementation "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"

}

tasks.named('test', Test) {
  useJUnitPlatform()
  jvmArgs += '-XX:+EnableDynamicAgentLoading'
  testLogging {
    events("passed", "skipped", "failed")
  }
}

checkstyle {
  toolVersion = '10.17.0'
  showViolations = false

  def archive = configurations.checkstyle.filter {
    it.name.startsWith("checkstyle")
  }
  config = resources.text.fromArchiveEntry(archive, "google_checks.xml")
}

spotless {
  java {
    target 'src/*/**/*.java'
    googleJavaFormat()
    licenseHeader '''\
    /*
     * Copyright (c) $YEAR The Finnish Ministry of Education and Culture, The Finnish
     * The Ministry of Economic Affairs and Employment, The Finnish National Agency of
     * Education (Opetushallitus) and The Finnish Development and Administration centre
     * for ELY Centres and TE Offices (KEHA).
     *
     * Licensed under the EUPL-1.2-or-later.
     */

     '''.stripIndent()
  }
}

jacoco {
  toolVersion = "0.8.12"
}

test.finalizedBy jacocoTestReport

jacocoTestReport {
  reports.xml.required = true
  reports.html.required = true
}

spotbugs {
  toolVersion = spotbugsVersion
  excludeFilter = file('gradle/config/spotbugs/exclusions.xml')
}

spotbugsMain {
  reports {
    html {
      required = true
    }
    xml {
      required = true
    }
  }
}

spotbugsTest.enabled = false

snyk {
  autoDownload = true
  autoUpdate = true
}

bootJar {
  manifest {
    attributes('Implementation-Title': "${rootProject.name}",
        'Implementation-Version': "${project.version}")
  }
}

springBoot {
  buildInfo {
    excludes = ['time']
  }
}

tasks.register('installGitHooks', Copy) {
  from("${rootProject.rootDir}/scripts/git-hooks") {
    include 'pre-*'
  }
  into "${rootProject.rootDir}/.git/hooks"
  filePermissions { unix(0775) }
}

if (System.getenv("CI") == null) {
  build {
    dependsOn 'installGitHooks'
  }
}

sonar {
  properties {
    property "sonar.projectKey", "Opetushallitus_jod-yksilo"
    property "sonar.organization", "opetushallitus"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.java.spotbugs.reportPaths", "build/reports/spotbugs/main.xml"
    property "sonar.cpd.exclusions", "src/**/entity/*.java"
  }
}

openApi {
  def port = new ServerSocket(0).withCloseable { socket -> socket.getLocalPort() }
  apiDocsUrl = "http://localhost:${port}/openapi/openapi.json"
  waitTimeInSeconds = 30
  customBootRun {
    environment = [
        COMPOSE_PROJECT_NAME: "jod-yksilo-openapi"
    ]
    args = ["--spring.main.lazy-initialization=true",
            "--server.port=${port}",
            "--logging.level.root=ERROR",
            "--logging.level.sql=OFF",
            "--logging.level.fi.okm.jod=ERROR",
            "--spring.config.additional-location=${projectDir}/src/test/resources/application-default.yml",
            "--spring.docker.compose.file=${projectDir}/compose-test.yml",
            "--spring.docker.compose.stop.command=down",
    ]
  }
}

tasks.named("dependencyUpdates").configure {
  checkConstraints = true
  checkBuildEnvironmentConstraints = true
  rejectVersionIf {
    it.candidate.version.containsIgnoreCase("alpha") || it.candidate.version.containsIgnoreCase("beta") || it.candidate.version.matches(".*-M\\d+")
  }
}
