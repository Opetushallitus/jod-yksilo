import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask

buildscript {
  dependencies {
    classpath("org.flywaydb:flyway-database-postgresql:${flywayVersion}")
  }
}

plugins {
  id 'java'
  id 'jacoco'

  id 'org.springframework.boot'
  id 'io.spring.dependency-management'

  id 'checkstyle'
  id "com.diffplug.spotless"
  id "com.github.spotbugs"
  id "org.sonarqube" version "6.3.1.5724"
  id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'

  id "com.github.ben-manes.versions" version "0.53.0"
  id "org.flywaydb.flyway" version "${flywayVersion}"
}

group = 'fi.okm.jod'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

compileJava {
  options.compilerArgs << '-Xlint:all,-processing,-serial,-classfile,-rawtypes'
}

repositories {
  mavenCentral()
  maven { url = "https://build.shibboleth.net/nexus/content/repositories/releases/" }
}

ext {
  spotbugsVersion = "4.9.8"
}

configurations {
  compileClasspath {
    resolutionStrategy.activateDependencyLocking()
  }
  runtimeClasspath {
    resolutionStrategy.activateDependencyLocking()
  }
  annotationProcessor {
    resolutionStrategy.activateDependencyLocking()
  }
  schemaSpy
  mockitoAgent
}

dependencies {
  implementation project(':tmt-profiili-api')
  implementation 'org.springframework.boot:spring-boot-starter-aspectj'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-webmvc'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
  implementation 'org.springframework.boot:spring-boot-starter-security-saml2'
  implementation 'org.springframework.boot:spring-boot-starter-restclient'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-session-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-opentelemetry'

  implementation 'org.springframework.boot:spring-boot-starter-flyway'
  implementation "org.flywaydb:flyway-database-postgresql"

  implementation platform('io.awspring.cloud:spring-cloud-aws-dependencies:4.0.0')
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-secrets-manager'
  implementation 'org.springframework.cloud:spring-cloud-context'
  implementation 'org.springframework.cloud:spring-cloud-commons'

  implementation platform('software.amazon.awssdk:bom:2.41.34')
  implementation 'software.amazon.awssdk:http-auth-aws'
  implementation 'software.amazon.awssdk:rds'
  implementation 'software.amazon.awssdk:sagemakerruntime'

  implementation 'com.google.guava:guava:33.5.0-jre'

  implementation "org.bouncycastle:bcpkix-jdk18on:${bouncyCastleVersion}"
  implementation "org.bouncycastle:bcprov-jdk18on:${bouncyCastleVersion}"

  // Caffeine cache implementation
  implementation 'com.github.ben-manes.caffeine:caffeine:3.2.3'

  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:3.0.1'
  developmentOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

  runtimeOnly 'org.postgresql:postgresql'

  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.hibernate.orm:hibernate-processor'
  annotationProcessor 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
  testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'

  testImplementation "org.testcontainers:testcontainers-postgresql"
  testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.apache.commons:commons-lang3'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  testAnnotationProcessor 'org.projectlombok:lombok'

  testCompileOnly 'org.projectlombok:lombok'

  // spotbugs
  implementation "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"

  schemaSpy 'org.schemaspy:schemaspy:7.0.2'
  schemaSpy 'org.postgresql:postgresql'

  mockitoAgent('org.mockito:mockito-core') {
    transitive = false
  }

}

tasks.named('test', Test) {
  useJUnitPlatform()
  maxHeapSize = "1g"
  jvmArgs += ['-XX:+EnableDynamicAgentLoading',
              "-javaagent:${configurations.mockitoAgent.asPath}"]
  systemProperty('file.encoding', 'UTF-8')
  testLogging {
    events("skipped", "failed")
  }
  maxParallelForks = 1
}

checkstyle {
  toolVersion = '13.0.0'
  showViolations = true

  def archive = configurations.checkstyle.filter {
    it.name.startsWith("checkstyle")
  }
  config = resources.text.fromArchiveEntry(archive, "google_checks.xml")
  configDirectory = file("${rootProject.rootDir}/gradle/config/checkstyle")
  configProperties = [
      'org.checkstyle.google.suppressionfilter.config': "${rootProject.rootDir}/gradle/config/checkstyle/suppressions.xml",
  ]
}

checkstyleMain {
  configProperties += [
      'org.checkstyle.google.severity': 'error',
  ]
}

spotless {
  java {
    target 'src/*/**/*.java'
    googleJavaFormat()
    licenseHeader '''\
    /*
     * Copyright (c) $YEAR The Finnish Ministry of Education and Culture, The Finnish
     * The Ministry of Economic Affairs and Employment, The Finnish National Agency of
     * Education (Opetushallitus) and The Finnish Development and Administration centre
     * for ELY Centres and TE Offices (KEHA).
     *
     * Licensed under the EUPL-1.2-or-later.
     */

     '''.stripIndent()
  }
}

jacoco {
  toolVersion = "0.8.14"
}

test.finalizedBy jacocoTestReport

jacocoTestReport {
  reports.xml.required = true
  reports.html.required = true
}

spotbugs {
  toolVersion = spotbugsVersion
  excludeFilter = file('gradle/config/spotbugs/exclusions.xml')
  jvmArgs = [
      '--sun-misc-unsafe-memory-access=allow',
      '--enable-native-access=ALL-UNNAMED',
  ]
}

spotbugsMain {
  reports {
    html {
      required = true
    }
    xml {
      required = true
    }
  }
}

spotbugsTest.enabled = false

bootJar {
  manifest {
    attributes('Implementation-Title': "${rootProject.name}",
        'Implementation-Version': "${project.version}")
  }
}

bootRun.dependsOn(":tmt-mock:build")

springBoot {
  buildInfo {
    excludes = ['time']
  }
}

tasks.register('installGitHooks', Copy) {
  from("${rootProject.rootDir}/scripts/git-hooks") {
    include 'pre-*'
  }
  into "${rootProject.rootDir}/.git/hooks"
  filePermissions { unix(0775) }
}

if (System.getenv("CI") == null) {
  build {
    dependsOn 'installGitHooks'
  }
}

sonar {
  properties {
    property "sonar.projectKey", "Opetushallitus_jod-yksilo"
    property "sonar.organization", "opetushallitus"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.java.spotbugs.reportPaths", "build/reports/spotbugs/main.xml"
    property "sonar.cpd.exclusions", "src/**/entity/*.java"

    //global issue exclusions (due to not compatible with e.g. spotless)
    property "sonar.issue.ignore.multicriteria", "e1"
    property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S8445"
    property "sonar.issue.ignore.multicriteria.e1.resourceKey", "src/**/*.java"
  }
}

openApi {
  def port = new ServerSocket(0).withCloseable { socket -> socket.getLocalPort() }
  outputDir = file("${projectDir}/build/openapi")
  apiDocsUrl = "http://localhost:${port}/yksilo/openapi/openapi.json"
  waitTimeInSeconds = 30
  customBootRun {
    // for netty, see https://github.com/netty/netty/wiki/Java-24-and-sun.misc.Unsafe
    jvmArgs = [
        '--sun-misc-unsafe-memory-access=allow',
        '--enable-native-access=ALL-UNNAMED',
    ]
    environment = [COMPOSE_PROJECT_NAME: "jod-yksilo-openapi"]
    args = ["--spring.main.lazy-initialization=true",
            "--server.port=${port}",
            "--logging.level.root=ERROR",
            "--logging.level.sql=OFF",
            "--logging.level.fi.okm.jod=ERROR",
            "--spring.config.additional-location=${projectDir}/src/test/resources/application-default.yml",
            "--spring.docker.compose.file=${projectDir}/compose-test.yml",
            "--spring.docker.compose.stop.command=down",]
  }
}

boolean hasMajorVersion(String version) {
  return !project.hasProperty("allowMajorUpdates") && version.split("\\.").length >= 3;
}

static int majorVersion(String version) {
  return Integer.valueOf(version.split("\\.")[0]);
}

static boolean isPreReleaseVersion(String version) {
  return version.containsIgnoreCase("alpha")
      || version.containsIgnoreCase("beta")
      || version.matches(".*[.-](?i:M)\\d+")
      || version.matches(".*[.-](?i:RC)\\d+")
      || version.matches(".*[.-](?i:CR)\\d+")
}

tasks.named("dependencyUpdates", DependencyUpdatesTask).configure {
  checkConstraints = true
  checkBuildEnvironmentConstraints = true
  rejectVersionIf {
    !isPreReleaseVersion(it.currentVersion) && isPreReleaseVersion(it.candidate.version)
        || (hasMajorVersion(it.currentVersion)
        && majorVersion(it.candidate.version) > majorVersion(it.currentVersion))
  }
}

tasks.register("generateSchemaDocs", JavaExec) {
  classpath = configurations.schemaSpy
  mainClass = 'org.schemaspy.Main'
  outputs.dir("build/schemaspy")
  args = ['-t', 'pgsql11',
          '-host', 'localhost',
          '-port', '45432',
          '-db', 'yksilo',
          '-u', 'postgres',
          '-p', 'postgres',
          '-s', 'yksilo',
          '-o', 'build/schemaspy',
          '-imageformat', 'svg',
          '-hq',
          '-vizjs']

  doFirst {
    def port = new ServerSocket(0).withCloseable { socket -> socket.getLocalPort() }
    def builder = new ProcessBuilder(javaLauncher.get().executablePath.toString(),
        "-cp",
        bootRun.classpath.asPath,
        bootRun.mainClass.get(),
        "--spring.main.lazy-initialization=true",
        "--spring.docker.compose.file=${projectDir}/compose-test.yml",
        "--spring.docker.compose.stop.command=down",
        "--server.port=${port}")
        .redirectError(file("build/tmp/application-error.log"))
        .redirectOutput(file("build/tmp/application-output.log"))
    builder.environment().put("COMPOSE_PROJECT_NAME", "jod-yksilo-schema")
    ext.process = builder.start()
    sleep(20000)
  }
  dependsOn tasks.resolveMainClassName, tasks.compileJava
  doLast {
    ext.process.destroy()
  }
}

flyway {
  defaultSchema = 'yksilo'
  schemas = ['yksilo', 'koulutusmahdollisuus_data', 'tyomahdollisuus_data', 'esco_data']
  outOfOrder = true
}

tasks.register('validateFlywayMigrationNames') {
  description = 'Validates that Flyway migration file names follow the pattern V<YYYYMMDD>.<issueid>__description.sql'
  group = 'verification'

  def migrationDir = file('src/main/resources/db/migration')
  inputs.dir(migrationDir)

  doLast {
    def pattern = ~/(R__\d{3}_.*.sql)|(?U:^[VB]\d{4}[01]\d[0-3]\d(\.\d+)+__\w+\.sql)$/

    if (migrationDir.exists()) {
      migrationDir.listFiles().each { file ->
        if (file.isFile() && file.name.endsWith('.sql')) {
          if (!pattern.matcher(file.name).matches()) {
            throw new GradleException("Invalid migration file name: ${file.name}")
          }
        }
      }
    }
  }
}

// suppress warnings due to Netty
// see https://github.com/netty/netty/wiki/Java-24-and-sun.misc.Unsafe

tasks.withType(Test).configureEach {
  jvmArgs = [
      '--sun-misc-unsafe-memory-access=allow',
      '--enable-native-access=ALL-UNNAMED',
  ]
}

tasks.withType(JavaExec).configureEach {
  jvmArgs = [
      '--sun-misc-unsafe-memory-access=allow',
      '--enable-native-access=ALL-UNNAMED',
  ]
}

check.dependsOn validateFlywayMigrationNames
test.shouldRunAfter validateFlywayMigrationNames
